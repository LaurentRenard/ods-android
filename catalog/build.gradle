apply plugin: 'com.android.application'

dependencies {
  api 'com.google.dagger:dagger:2.28.1'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.28.1'

  api 'com.google.dagger:dagger-android:2.28.1'
  api 'com.google.dagger:dagger-android-support:2.28.1'
  annotationProcessor 'com.google.dagger:dagger-android-processor:2.28.1'

  api 'androidx.multidex:multidex:2.0.1'
  api 'androidx.constraintlayout:constraintlayout:1.1.3'
  api 'androidx.gridlayout:gridlayout:1.0.0'

  api 'com.google.guava:guava:27.0.1-android'

  api project(':lib')

//  api compatibility("recyclerViewSelection")
  api 'androidx.recyclerview:recyclerview-selection:1.0.0'
  def testRunnerVersion = '1.1.0'
  def espressoVersion = '3.1.0'
  def mockitoCoreVersion = '2.25.0'
  def truthVersion = '0.45'

  androidTestImplementation "androidx.test:core:${testRunnerVersion}"
  androidTestImplementation "androidx.test:runner:${testRunnerVersion}"
  androidTestImplementation "androidx.test:rules:${testRunnerVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"

  testImplementation "androidx.test:core:${testRunnerVersion}"
  testImplementation "androidx.test:runner:${testRunnerVersion}"
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:${truthVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
  testImplementation "org.robolectric:robolectric:4.3"

  testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc4"

}

def srcDirs = [
  'application',
  'application/attrs',
  'application/legacymultidex',
  'application/scope',
  'application/theme',
  'assets',
  'bottomappbar',
  'bottomnav',
  'bottomsheet',
  'button',
  'card',
  'checkbox',
  'chip',
  'datepicker',
  'dialog',
  'draggable',
  'elevation',
  'imageview',
  'fab',
  'feature',
  'font',
  'internal',
  'main',
  'menu',
  'navigationrail',
  'progressindicator',
  'radiobutton',
  'shapetheming',
  'slider',
  'switchmaterial',
  'tableofcontents',
  'tabs',
  'textfield',
  'themeswitcher',
  'timepicker',
  'topappbar',
  'transition',
  'transition/hero',
  'windowpreferences'
]

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.androidTools

  defaultConfig {
    manifestPlaceholders = [
      application_name             : 'CatalogApplication',
      application_theme            : 'Catalog',
      catalog_application_component: 'io.material.catalog.application.DaggerCatalogApplicationComponent'
    ]

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    minSdkVersion 21
    targetSdkVersion 30
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  sourceSets {
    main {
      manifest.srcFile 'java/io/material/catalog/AndroidManifest.xml'
      java.srcDir 'java'
      java.includes = srcDirs.collect {
        'io/material/catalog/' + it + '/**/*.java'
      }
      java.excludes = [
        '**/build/**',
      ]
      res.srcDirs = ['java/io/material/catalog/res']
      srcDirs.forEach {
        res.srcDirs += 'java/io/material/catalog/' + it + '/res'
      }
    }
    //TODO(b/141695010)
    // androidTest.java.srcDir 'androidTest/javatests'
    // test.java.srcDir 'test/javatests'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}
