/*
 *
 *  Copyright 2021 Orange
 *
 *  Use of this source code is governed by an MIT-style
 *  license that can be found in the LICENSE file or at
 *  https://opensource.org/licenses/MIT.
 * /
 */

buildscript {
    repositories {
        google()
    }

    ext.nav_version = "2.3.5"

    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.androidTools

    defaultConfig {
        minSdkVersion versions.minSdk
        applicationId "com.orange.ods.sample"
        targetSdkVersion versions.compileSdk
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        release {
            storeFile file("sample.jks")
            storePassword "password"
            keyAlias "key"
            keyPassword "password"
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            ext.enableCrashlytics = false
        }

        release {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        releaseUnsigned.initWith(buildTypes.release)
        releaseUnsigned {
            signingConfig null
            matchingFallbacks = ["release"]
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions.jvmTarget = jvm_target

    buildFeatures {
        dataBinding = true
    }

    lintOptions {
        checkDependencies true
    }
}

dependencies {

    implementation project(path: ':lib')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.core:core-ktx:1.6.0"

    def activity_version = "1.3.0"
    implementation "androidx.activity:activity-ktx:$activity_version"

    def fragment_version = "1.3.6"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:28.3.0')

    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}